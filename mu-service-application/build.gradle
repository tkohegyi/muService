description= 'muService'

task checkstyle(type: Checkstyle) {
    dependsOn subprojects.checkstyleMain
}

def releaseCopySpec = project.copySpec {
    from(rootProject.rootDir.absolutePath + '/mu-service-application/modules/mu-service-engine/') {
        include 'mu-service.conf.properties'
        include 'readme.txt'
    }
    from(rootProject.rootDir.absolutePath + '/config/'){
        include 'security/adorApp.cer'
        include 'security/adorApp.keystore'
    }
    from(rootProject.rootDir.absolutePath + '/mu-service-application/modules/mu-service-engine/build/libs/') {
        include "mu-service-engine-all.jar"
    }
}

task copyToRelease(type: Copy) {
    dependsOn ":mu-service-application:mu-service-engine:shadowJar"
    into rootProject.rootDir.absolutePath + '/release'
    with releaseCopySpec
    rename "mu-service-engine-all.jar", "mu-service-" + "${rootProject.version}" + ".jar"
}

task release(type: Zip) {
    dependsOn(copyToRelease)
    from rootProject.rootDir.absolutePath + '/release'
}

subprojects {
    repositories {
        maven { url "https://repository.jboss.org/nexus/content/groups/public" }
    }
    dependencies {
        implementation (group: 'org.springframework', name: 'spring-context', version:"${springFrameworkVersion}") {
            exclude(module: 'commons-logging')
        }
        implementation group: 'org.springframework', name: 'spring-webmvc', version: "${springFrameworkVersion}"
        implementation group: 'org.aspectj', name: 'aspectjrt', version:'1.6.12'
        implementation group: 'org.postgresql', name: 'postgresql', version:'42.2.20'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: "${mockitoCoreVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoCoreVersion}"
        testImplementation "junit:junit:${jUnitVersion}"
        testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.9'
        testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'
    }
}
